name: The Ninth Trial
object who:
    lady
    tiger
object room:
    _1
    _2
    _3
predicate sign_is_true(room)
predicate contains(room, who)
rules:
    # There can be only a lady or a tiger inside each room.
    for every room X (contains(X, lady) <=> !contains(X, tiger))
    # There is one lady and two tigers
    for every room X (contains(X, lady) => for every room Y except X (contains(Y, tiger)))
    # At most one of the three signs is true.
    for every room X (sign_is_true(X) => for every room Y except X (!sign_is_true(Y)))
    # 1. A tiger is in this room.
    sign_is_true(_1) <=> contains(_1, tiger)
    # 2. A lady is in this room.
    sign_is_true(_2) <=> contains(_2, lady)
    # 3. A tiger is in room 2.
    sign_is_true(_3) <=> contains(_2, tiger)
